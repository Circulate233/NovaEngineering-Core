buildscript {
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'net.minecraftforge.gradle' version '5.+'
    id 'wtf.gofancy.fancygradle' version '1.+'
}

apply plugin: 'org.spongepowered.mixin'

group = 'github.kasuminova'
version = '1.4'

repositories {
    flatDir {
        dirs 'lib'
    }
    maven {
        url 'https://maven.cleanroommc.com'
    }
    maven {
        url 'https://cfa2.cursemaven.com'
    }
    maven {
        url 'https://cursemaven.com'
    }
    maven {
        url 'https://maven.blamejared.com/'
    }
    maven {
        url 'https://maven.hellfiredev.net/'
    }
}

java {
    archivesBaseName = 'NovaEngineering-Core'
    toolchain.languageVersion = JavaLanguageVersion.of(8)
}

minecraft {
    mappings channel: 'stable', version: '39-1.12'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                novaengineering_core {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                novaengineering_core {
                    source sourceSets.main
                }
            }
        }
    }
}

fancyGradle {
    patches {
        resources
        coremods
        codeChickenLib
        asm
        mergetool
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

mixin {
    add sourceSets.main, "mixins.novaeng_core.refmap.json"
//    add sourceSets.another, "another.refmap.json"
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    // Mixins
    compileOnly("zone.rong:mixinbooter:7.0")
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    // Mod Dependencies
    implementation fg.deobf('hellfirepvp:modularmachinery:r45:main')
    implementation "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.+"
    compileOnly fg.deobf("curse.maven:zenutil-401178:4394263")
    compileOnly fg.deobf("curse.maven:libvulpes-236541:3801015")
    compileOnly fg.deobf("curse.maven:advanced-rocketry-236542:4671856")
    compileOnly fg.deobf("curse.maven:immersive-engineering-231951:2974106")
    compileOnly fg.deobf("curse.maven:environmental-tech-245453:2691536")
    compileOnly fg.deobf("curse.maven:nuclearcraft-overhauled-336895:3862197")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "novaengineering_core",
                "Specification-Vendor"    : "Kasumi_Nova",
                "Specification-Version"   : "1.0",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Kasumi_Nova",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
